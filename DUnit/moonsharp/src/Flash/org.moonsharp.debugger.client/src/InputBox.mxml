<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
			   xmlns:s="library://ns.adobe.com/flex/spark" 
			   xmlns:mx="library://ns.adobe.com/flex/mx" width="728" height="142"
			   creationComplete="onLoaded(event)"
			   defaultButton="{btnOK}"
			   >
	
	<fx:Script>
		<![CDATA[
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import spark.events.TextOperationEvent;
			
			private var m_CallBackFunc:Function = null;
			private var m_Closed:Boolean = false;
			private var m_Label:String;
			private var m_Default:String;
			
			public static function show(parentDisplayObject:DisplayObject, title:String, label:String, defaultText:String, callBack:Function):InputBox
			{
				var win:InputBox = PopUpManager.createPopUp(parentDisplayObject, InputBox, true) as InputBox;
				
				win.m_CallBackFunc = callBack;
				win.m_Label = label;
				win.m_Default = defaultText;
				win.title = (title != null) ? title : "";
				
				PopUpManager.centerPopUp(win);
				
				return win;
			}
			
			protected function txtText_changingHandler(event:TextOperationEvent):void
			{
				btnOK.enabled = (txtText.text.length > 0);
			}
			
			protected function btnOK_clickHandler(event:MouseEvent):void
			{
				if (!m_Closed) PopUpManager.removePopUp(this); 
				m_Closed = true; 
				m_CallBackFunc(txtText.text);
			}
			
			protected function btnCancel_clickHandler(event:MouseEvent):void
			{
				if (!m_Closed) PopUpManager.removePopUp(this); 
				m_Closed = true; 
				
				m_CallBackFunc(null);
			}			
			
			protected function onLoaded(event:FlexEvent):void
			{
				txtText.text = m_Default;
				lblHelp.text = m_Label;
				btnOK.enabled = (txtText.text.length > 0);
				txtText.selectAll();
				focusManager.setFocus(txtText);
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<s:TextInput id="txtText" x="52" y="56" width="518" change="txtText_changingHandler(event)" changing="txtText_changingHandler(event)" />
	<s:Button id="btnOK" x="626" y="29" width="90" height="31" label="OK" click="btnOK_clickHandler(event)" />
	<s:Button id="btnCancel" x="626" y="68" width="90" height="31" label="Cancel" click="btnCancel_clickHandler(event)"/>
	<s:Label id="lblHelp" x="19" y="25" width="551" height="23" text="Label"/>
	
</s:TitleWindow>
